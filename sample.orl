module Sample where {
  quantity Length;
  quantity Time;
  quantity Mass;
  quantity Electric_current;
  quantity Temperature;
  quantity Luminous_intensity;
  quantity Amount_of_substance;
  quantity Angle;

  -- SI base units
  unit meter alias m of Length;
  unit kilogram alias kg of Mass;
  unit second alias s of Time;
  unit ampere alias A of Electric_current;
  unit kelvin alias K of Temperature;
  unit candela alias cd of Luminous_intensity;
  unit mole alias mol of Amount_of_substance;

  -- angles
  unit radian ≙ 1; – the unit "radian" is an alias for a dimensionless value

  unit degree of Angle;
  unit revolution of Angle;
  unit gradian of Angle;

  conversion 360 〈degree〉  per 1 〈revolution〉;
  conversion 400 〈gradian〉 per 1 〈revolution〉;
  conversion 2·π  per 1 〈revolution〉;

  -- other units
  unit minute alias min of Time;
  unit hour alias hr of Time;

  conversion 60 〈s〉 per 1 〈min〉;
  conversion 60 〈min〉 per 1 〈hr〉;

  unit centimeter alias cm of Length;
  unit milimeter alias mm of Length;

  unit foot alias ft of Length;
  unit inch alias in of Length;
  unit mile of Length;

  conversion 5280 〈ft〉 per 1 〈mile〉;
  conversion 12 〈in〉 per 1〈ft〉;
  conversion 2.54 〈cm〉 per 1 〈in〉;
  conversion 100 〈cm〉 per 1 〈m〉;
  conversion 1000 〈mm〉 per 1 〈m〉;

  unit hertz alias Hz ≙ s⁻¹;
  unit columb alias C ≙ A·s;
  unit newton alias N ≙ kg·m/s²;
  unit joule alias J ≙ N·m;
  unit watt alias W ≙ J/s;
  unit volt alias V ≙ J/C;
  unit ohm alias Ω ≙ V/A;
  unit henry alias H ≙ Ω·s;
  unit farad alias F ≙ C/V;

  primitive sin : ℝ → ℝ;
  primitive cos : ℝ → ℝ;
  primitive tan : ℝ → ℝ;
  primitive arcsin : ℝ → ℝ;
  primitive arccos : ℝ → ℝ;
  primitive arctan : ℝ → ℝ;

  primitive exp : ℝ → ℝ;
  primitive ln : ℝ → ℝ;

  symbolic constant half_circle_ratio alias π
      – we define π via Machin's formula, which converges much faster than 4·arctan(1)
      ≙ 16·arctan(1/5) − 4·arctan(1/239);

  symbolic constant speed_of_light alias c
      ≙ 299_792_458 〈m/s〉;

  symbolic constant plank_constant alias h
      ≙ 6.626_069_5729_E-34 〈J·s〉;

  symbolic constant avagadro_number alias Nₐ
      ≙ 6.022_141_E+23 〈mol⁻¹〉;

  symbolic constant faraday_constant alias F
      ≙ 1.602_1766_E+4 〈C/mol〉;

  symbolic constant gravitational_constant alias G
      ≙ 6.673_E-11 〈N·m²·kg⁻²〉;

  symbolic constant standard_earth_gravity alias gₑ
      ≙ 9.80665 〈m/s²〉;

--  constant bogus_constant
--      ≙ 10^(6 / (0 · 10)) 〈1/m〉;

  constant magnetic_constant alias μ₀
      ≙ π · 4E-7 〈H·s〉;
  constant electric_constant alias ε₀
      ≙ 1/(μ₀ · c²);
  constant dirac_constant alias ℏ
      ≙ h / (2·π);
  constant elementary_charge alias e
      ≙ F / Nₐ;
  constant fine_structure_constant alias α
      ≙ e²·c·μ₀ / 2·h;


definition gravational_attraction (m₁ m₂:ℝ〈kg〉) (r:ℝ〈m〉) : ℝ〈N〉
   ≙ (G·m₁·m₂)/r²;


--gravitational_attraction : ℝ〈kg〉 → ℝ〈kg〉 → ℝ〈m〉 → ℝ〈N〉;
--gravitational_attraction ≙ 𝛌m₁ m₂ r, (G·m₁·m₂)/r²;


– geometric space complex
–  units (1)
–  basis (《i》)
–  signature (-1);

–  geometric space D1
–     units ( second, meter )
–     basis (《t》,《x》)
–     signature ( c, -1 );

–  scale_d1t : ∀(α β:Unit) (k:Grade), Real〈α〉 → D1(k)〈β〉 → D1(k)〈α·β〉;
–  scale_d1t x v ≙ x·v;
  
–  vector_mult : ∀(α β:Unit), D1{1}〈α〉 → D1{1}〈β〉 → D1{0,2}〈α·β〉
–  vector_mult ≙ 𝛌x y, x·y;

–  fact : 《t》= D1( 1, 0 );
–  fact : 《x》= D1( 0, 1 );
}
